// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Zero the x input?
    Mux16(a=x, b=false, sel=zx, out=zeroX);

    // Negate the x input?
    Not16(in=zeroX, out=notZeroX);
    Mux16(a=zeroX, b=notZeroX, sel=nx, out=negateX);

    // Zero the y input?
    Mux16(a=y, b=false, sel=zy, out=zeroY);

    // Negate the y input?
    Not16(in=zeroY, out=notZeroY);
    Mux16(a=zeroY, b=notZeroY, sel=ny, out=negateY);

    //compute (out = x + y) or (out = x & y)?
    And16(a=negateX, b=negateY, out=andXY);
    Add16(a=negateX, b=negateY, out=addXY);
    Mux16(a=andXY, b=addXY, sel=f, out=compute);

    // negate the out output?
    Not16(in=compute, out=notCompute);
    Mux16(a=compute, b=notCompute, sel=no, out[0..7]=outFirstHalf);
    Mux16(a=compute, b=notCompute, sel=no, out[8..15]=outSecondHalf);
    Or16(a[0..7]=outFirstHalf, b[8..15]=outSecondHalf, out=out);

    //Compute zr
    Or8Way(in=outFirstHalf, out=orOutFirstHalf);
    Or8Way(in=outSecondHalf, out=orOutSecondHalf);
    Or(a=orOutFirstHalf, b=orOutSecondHalf, out=orOut);
    Not(in=orOut, out=zr);

    //Compute ng
    And16(a[8..15]=outSecondHalf, b[8..15]=outSecondHalf, out[15]=ng);
}